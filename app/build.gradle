apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "de.mannodermaus.android-junit5"

apply from: rootProject.file('dependencies.gradle')

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId names.applicationId
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }
        unitTests.all {
            testLogging.events = ["passed", "skipped", "failed"]
        }
    }
}

dependencies {
    implementation project(":core")
    implementation project(":features:authentication")
    testImplementation project(":test")

    implementation deps.stdlib
    implementation deps.core_ktx

    // Dagger 2
    implementation deps.dagger_dagger
    kapt deps.dagger_compiler

    // Navigation components
    implementation deps.navigation_fragment
    implementation deps.navigation_ui

    // Timber
    implementation deps.timber

    // Spek 2
    testImplementation deps.spek2_jvm
    testImplementation deps.spek2_junit5

    // Kotlin reflect for Spek 2
    testImplementation deps.kotlin_reflect

    // Junit 5
    androidTestImplementation deps.android_test_runner
    androidTestImplementation deps.junit5_core
    androidTestRuntimeOnly deps.junit5_runner

    // AssertJ
    testImplementation deps.assertJ

    // MockitoKotlin
    testImplementation deps.mockitoKotlin

    // MockWebServer
    testImplementation deps.mockWebServer
}
